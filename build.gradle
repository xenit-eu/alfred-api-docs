plugins {
    id 'eu.xenit.docker-compose' version '5.0.7'
}

dockerCompose {
    useComposeFiles = ['docker-compose.yml']
    captureContainersOutput = true
}

task initCreateDocs() {
    doLast {
        copyResources('user')
    }
}

// some weird chaining, but it works :-)
//      dixit Daan Kerkhofs anno 2018-01-08
composeUp.dependsOn initCreateDocs
composeDown.dependsOn composeUp

task createDocs(type: Copy) {
    dependsOn composeDown

    from "${project.buildDir}/src"
    into "${project.buildDir}/"
    include '**/*.pdf'
    includeEmptyDirs = false

    // Copy only files, remove folder structure
    eachFile {
        details -> details.path = details.name
    }

    doLast {
        delete fileTree(dir: "${project.buildDir}/src/")
    }
}

task clean(type: Delete) {
    delete project.buildDir
}

def copyResources(String source) {
    def targetDir = "${project.buildDir}/src/${source}"

    copy {
        from source
        into targetDir
    }
}
